apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'signing'
apply plugin: 'maven'

repositories {
  mavenCentral()
}

dependencies {
  compile 'org.apache.httpcomponents:httpclient:4.3.5'
  compile 'com.google.code.gson:gson:2.3'
  compile 'com.sparkjava:spark-core:1.1.1'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

signing {
  sign configurations.archives
}

mavenPomDir = "build/libs"

uploadArchives {
  repositories {
    mavenDeployer {
      repository(url: '')
      pom.version = '0.2.0'
      pom.groupId = 'com.playlyfe'
      pom.artifactId = 'playlyfe-java-sdk'
      pom.project {
        name 'Playlyfe Java SDK'
        description 'This is the official OAuth 2.0 Java client SDK for the Playlyfe API. It supports the client_credentials and authorization code OAuth 2.0 flows.'
        url 'https://github.com/playlyfe/playlyfe-java-sdk'
        inceptionYear '2014'

        scm {
          url 'https://github.com/playlyfe/playlyfe-java-sdk'
          connection 'scm:git@github.com:playlyfe/playlyfe-java-sdk.git'
          developerConnection 'scm:git@github.com:playlyfe/playlyfe-java-sdk.git'
        }
        licenses {
          license {
              name 'The MIT License (MIT)'
              url 'https://github.com/playlyfe/playlyfe-java-sdk/blob/master/LICENSE.md'
              distribution 'repo'
          }
        }

        developers {
          developer {
              id 'playlyfe'
              name 'Peter John'
              email 'peter@playlyfe.com'
          }
        }
      }
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
    }
  }
}
